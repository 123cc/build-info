
apply plugin: 'java'
apply plugin: 'groovy'
apply plugin: 'idea'

version = '1.0'
description = 'Build Info itest'
println description

allprojects {
    sourceCompatibility = 1.7
    targetCompatibility = 1.7

    repositories {
        jcenter()
        maven {
            url "https://oss.jfrog.org/jfrog-dependencies"
        }
    }
}

configurations {
    uberJar {
        description = 'All jars that should be collected'
        transitive = true
    }
}

/* Collect all the jars that were recorded by the "uberJar" configuration.
 * In our case we record buildInfo Maven and Ivy extractors modules with all their dependencies.
 * We use is for Maven and Ivy tests
 */
task buildInfoClassPath(type: Copy) {
    from (configurations.uberJar)
        into project.file('src/test/resources/cache/artifactory-plugin')
}

assemble {
    dependsOn buildInfoClassPath
}

dependencies {
    uberJar(group:'org.jfrog.buildinfo', name:'build-info-extractor-ivy', version:'+'){
        exclude group:'org.codehaus.groovy', module:'groovy-all'
    }
    uberJar(group:'org.jfrog.buildinfo', name:'build-info-extractor-maven3', version:'+'){
        exclude group:'org.codehaus.groovy', module:'groovy-all'
        exclude group:'org.apache.maven'
    }
    compile(group:'org.jfrog.buildinfo', name:'build-info-extractor-ivy', version:'+')
    compile(group:'org.jfrog.buildinfo', name:'build-info-extractor-maven3', version:'+'){
        exclude group:'org.codehaus.groovy', module:'groovy-all'
    }
    compile(group:'org.jfrog.buildinfo', name:'build-info-api', version:'+')
    compile(group:'org.jfrog.artifactory.client', name:'artifactory-java-client-api', version:'+')
    compile(group:'org.jfrog.artifactory.client', name:'artifactory-java-client-services', version:'+')
    compile(group:'org.spockframework', name:'spock-core', version:'1.0-groovy-2.3')
    compile(group:'org.apache.ant', name:'ant', version:'1.9.2')
    compile(group:'org.jfrog.util', name:'docker-remote-util', version:'0.1'){
        exclude group:'org.codehaus.groovy', module:'groovy-all'
    }
}

test {
    //Spock run with JUnit
    test.useJUnit()
    exclude 'build/specs'

    testLogging {
        exceptionFormat "full"
        events "started", "passed", "skipped", "failed", "standardOut", "standardError"
        minGranularity 0
    }

    System.getProperties().each{
        systemProperties.put(it.key, it.value)
    }
}

task wrapper(type: Wrapper) {
    description = 'Generates gradlew[.bat] scripts'
    gradleVersion = '2.4'
}